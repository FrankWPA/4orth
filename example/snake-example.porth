include "wasm4.porth"

// Game state
memory frame-count sizeof(int) end
memory prev-state  sizeof(u8)  end

// Game consts
const sizeof(coord) sizeof(int) 2 * end
const sizeof(cell)   8 end
const sizeof(grid)  20 end
const delta_time     5 end 
const SNAKE_CAP     32 end

// Helper pointer offsets
inline proc x+ ptr -- ptr in end 
inline proc y+ ptr -- ptr in sizeof(int) ptr+ end
inline proc !x int ptr in !int end
inline proc !y int ptr in y+ !int end
inline proc @x ptr -- int in @int end
inline proc @y ptr -- int in y+ @int end

inline proc snake-incBy ptr int -- ptr in sizeof(coord) * ptr+ end
inline proc snake++     ptr     -- ptr in 1 snake-incBy end

// Snake values
const snake.length sizeof(int)   offset end
const snake.dir    sizeof(coord) offset end
const snake.body   sizeof(coord) SNAKE_CAP * offset end
const sizeof(snake) reset end

memory snake sizeof(snake) end

// Snake values setters and getters
inline proc @snake-length -- int in snake snake.length ptr+ @int end
inline proc !snake-length    int in snake snake.length ptr+ !int end
inline proc length++ in @snake-length 1 + !snake-length end

inline proc snake-head -- ptr in snake snake.body ptr+ end
inline proc snake-tail -- ptr in snake-head @snake-length sizeof(coord) * ptr+ end

inline proc snake-dir   -- ptr in snake snake.dir ptr+ end
inline proc !snake-dir int int in snake-dir !y snake-dir !x end

inline proc ?movingx -- bool in snake-dir @x 0 != end
inline proc ?movingy -- bool in snake-dir @y 0 != end

// Fruit values
memory fruit sizeof(coord) end

const fruit-sprite "\\00\\a0\\02\\00\\0e\\f0\\36\\5c\\d6\\57\\d5\\57\\35\\5c\\0f\\f0"c end
const fruit-flags $BLIT_2BPP end
const fruit-height 8 end
const fruit-width  8 end

// Helper inline procs
inline proc rnd-coord -- int int in 20 rnd 20 rnd end
inline proc ptr> ptr ptr -- bool in swap ptr< end

// Wasm4 memory getters and setters
inline proc !PALETTE int int in 4 % 4 * $PALETTE0 +ptr !int end
inline proc !COLORS int in $DRAW_COLORS !16 end
inline proc @GAMEPAD -- int in $GAMEPAD1 @8 end

// Initialize fruit in a random position
inline proc new-fruit in 
  rnd-coord
  fruit !x
  fruit !y 
end

// Initialize snake body and direction
inline proc snake-init in
  1 0 !snake-dir
  3 !snake-length

  @snake-length snake-head 
  while over 0 > do
    let xPos snake* in
      xPos snake* !x
      10   snake* !y
      xPos   1 -
      snake* snake++
    end
  end drop drop
end

proc init in
  snake-init
  new-fruit
end

proc main in
  // Color palette setting
  0x151640 0 !PALETTE
  0x3f6d9e 1 !PALETTE
  0xf783b0 2 !PALETTE
  0xe6f2ef 3 !PALETTE

  init
end

// Change snake direction based on input xdir and ydir.
// If value is 1 it's positive, otherwise it's negative
proc input-dir int int in
  let x y in
    y 0 != ?movingx land
    x 0 != ?movingy land lor if
      x y !snake-dir
    end
  end
end

// Compare current pressed keys with keys pressed in the last checked frame and calls input-dir
proc check-keys in
  @GAMEPAD 
  let keys in
    keys prev-state @8 xor keys and
    keys prev-state !8
    peek pressed in 0 != if
      pressed $BUTTON_LEFT  and 0 != if  2 0 
      else
      pressed $BUTTON_RIGHT and 0 != if* 1 0 
      else
      pressed $BUTTON_DOWN  and 0 != if* 0 1 
      else
      pressed $BUTTON_UP    and 0 != if* 0 2
      else 0 0 end input-dir
    end end
  end
end

// Return the wrapped snake position around the screen given its position and direction
proc clamp-wrap int int -- int in
  1 = let pos isPositive in
    sizeof(grid) 1 - 0       //screen limits
    isPositive if swap end   //check based on direction
    pos = lnot if drop       //if not at the limits, just increment/decrement
      pos 1 isPositive
      if + else - end 
    end
  end
end

// Update every part of the snake body to its new position
proc snake-update in
  snake-head @x 
  snake-head @y
  snake-head 
  while snake++ dup snake-tail ptr< do
    let x y snake* in
      snake* @x
      snake* @y
      y snake* !y
      x snake* !x
      snake* 
    end
  end drop drop drop

  ?movingx if
    snake-head @x snake-dir @x
    clamp-wrap
    snake-head !x
  else ?movingy if*
    snake-head @y snake-dir @y
    clamp-wrap
    snake-head !y
  end
end

// Check if the snake head is colliding with the fruit.
// Incrementing the snake and generating a new fruit if true.
proc check-fruit in
  snake-head @x fruit @x =
  snake-head @y fruit @y = land if
    length++
    new-fruit
  end
end

proc check-death -- bool in
  false snake-head 3 snake-incBy
  while over lnot over snake-tail ptr< land do
    let isDead snake* in
      snake* @x snake-head @x =
      snake* @y snake-head @y = land 
      snake* snake++
    end
  end drop
end

// Draw a 8 x 8 rectangle in a given coord
proc draw-rect int int in
  let x y in
    sizeof(cell) dup
    y sizeof(cell) *
    x sizeof(cell) *
    rect
  end
end

// Draws all parts of the snake
proc snake-draw in
  0x0004 !COLORS

  snake-head @x
  snake-head @y
  draw-rect

  0x0043 !COLORS

  snake-tail snake-head 
  while snake++ over over ptr> do
    peek snake* in
      snake* @x
      snake* @y draw-rect
    end
  end drop drop
end

// Draws the fruit sprite
proc fruit-draw in
  0x4320 !COLORS
  fruit-flags 
  fruit-height
  fruit-width
  fruit @y sizeof(cell) *
  fruit @x sizeof(cell) *
  fruit-sprite
  blit
end

// Gameloop
proc update in
  frame-count inc32

  // Only do checks and updates every `delta_time` frames
  frame-count @int delta_time % 0 = if
    check-keys
    check-fruit
    snake-update

    check-death if
      init
    end
  end

  fruit-draw
  snake-draw
end